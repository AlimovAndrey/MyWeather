apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.alimov.myweather"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {

        def stringType = 'String'

        def locationNameKey = "LOCATION_NAME"

        lviv {
            versionNameSuffix '(Lviv)'
            applicationIdSuffix ".lviv"

            addManifestPlaceholders([APP_NAME: "My Weather (Lviv)"])

            buildConfigField stringType, locationNameKey, '"Lviv"'

            dimension = 'default'
        }

        kiev {
            versionNameSuffix '(Kiev)'
            applicationIdSuffix ".kiev"

            addManifestPlaceholders([APP_NAME: "My Weather (Kiev)"])

            buildConfigField stringType, locationNameKey, '"Kiev"'

            dimension = 'default'
        }
    }
}

kapt {
    generateStubs = true
}

ext {
    supportVersion = '27.1.1'
    moxyVersion = '1.5.3'
    daggerVersion = '2.11'
    okhttp3Version = '3.9.0'
    retrofitVersion = '2.3.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    /* Support */
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"

    /* RX2 */
    implementation "io.reactivex.rxjava2:rxjava:2.1.3"
    implementation('io.reactivex.rxjava2:rxandroid:2.0.1') {
        exclude module: 'rxjava'
    }

    /* Dagger */
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    /* Retrofit */
    implementation("com.squareup.retrofit2:retrofit:$retrofitVersion") {
        exclude module: 'okhttp'
    }
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    /* Ok Http3 */
    implementation "com.squareup.okhttp3:okhttp:$okhttp3Version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp3Version"

    /* Moxy */
    implementation "com.arello-mobile:moxy:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
    implementation "com.arello-mobile:moxy-android:$moxyVersion"
    kapt "com.arello-mobile:moxy-compiler:$moxyVersion"


    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.20.1"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
